__numbertext__
^0 zero
1$ u
1 un
2 dos
3 tres
4 quatre
5 cinc
6 sis
7 set
8 vuit
9 nou
10 deu
11 onze
12 dotze
13 tretze
14 catorze
15 quinze
16 setze
17 disset
1(\d) di$1
20 vint
2(\d) vint-i-$1
30 trenta
40 quaranta
50 cinquanta
60 seixanta
70 setanta
80 vuitanta
90 noranta
(\d)(\d) $(\10)-$2
1(\d\d) cent $1
(\d)(\d\d) $1-cents $2
(\d{1,3})(\d{3}) $1 mil $2
1(\d{6}) un milió $1
(\d{1,6})(\d{6}) $1 milions $2
1(\d{9}) mil milions $1
1(\d{12}) un bilió $1
(\d{1,6})(\d{12}) $1 bilions $2
1(\d{18}) un trilió $1
(\d{1,6})(\d{18}) $1 trilions $2
1(\d{24}) un quadrilió $1
(\d{1,6})(\d{24}) $1 quadrilions $2

# negative number

[-−](\d+) menys |$1

# decimals

"([-−]?\d+)[.,]" $1| coma
"([-−]?\d+[.,]\d*)(\d)" $1| |$2

# currency

# convert masculine to feminine (<1000000)

f:(.*u)n([^a].*|$) $(f:\1na\2)	# un -> una
f:(.*d)o(s.*) $(f:\1ue\2)	# dos -> dues
f:(.*cent)(s.*) $(f:\1e\2)	# cents -> centes
.:(.*) \1

# unit/subunit singular/plural

# million or greater part of the number name separated by "ili" pattern
# before masculine to feminine conversion

us(.).:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*ili)?([^,]*) \6$(\1:\7) \2
up(.).:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*ili)?([^,]*) \6$(\1:\7) \3
ss.(.):([^,]*),([^,]*),([^,]*),([^,]*),([^,]*ili)?([^,]*) \6$(\1:\7) \4
sp.(.):([^,]*),([^,]*),([^,]*),([^,]*),([^,]*ili)?([^,]*) \6$(\1:\7) \5

# "mm" means masculine unit and masculine subunit

CHF:(.+),(.+) $(\2mm: franc suís, francs suís, cèntim, cèntims, \1)
ESP:(.+),(.+) $(\2fm: pesseta, pessetes, cèntim, cèntims, \1)
EUR:(.+),(.+) $(\2mm: euro, euros, cent, cents, \1)
GBP:(.+),(.+) $(\2fm: lliura esterlina, lliures esterlines, penic, penics, \1)
JPY:(.+),(.+) $(\2mm: ien, iens, sen, sen, \1)
USD:(.+),(.+) $(\2mm: dòlar EUA, dòlar EUA, cent, cents, \1)

"([A-Z]{3}) ([-−]?1)([.,]00?)?" $(\1:|$2,us)
"([A-Z]{3}) ([-−]?\d+)([.,]00?)?" $(\1:|$2,up)

"(([A-Z]{3}) [-−]?\d+)[.,](01)" $1 amb $(\2:un,ss)
"(([A-Z]{3}) [-−]?\d+)[.,](\d)" $1 amb $(\2:|$(\30),sp)
"(([A-Z]{3}) [-−]?\d+)[.,](\d\d)" $1 amb $(\2:|$3,sp)

