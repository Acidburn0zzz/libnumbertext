__numbertext__
^0 אפס
1 אחת
2 שתיים
3 שלוש
4 ארבע
5 חמש
6 שש
7 שבע
8 שמונה
9 תשע
10 עשר
12 שתים-עשרה
1(\d) $1-עשרה

20 עשרים
50 חמישים
60 שישים
80 שמונים
([^2568])0 $1ים

(\d)(\d) $(\10) ו$2

# separator after hundred
hs:0[^0]\d* ו # מאה ואחת
hs:1\d+ ו     # מאה ועשר
hs:20\d* ו    # מאה ועשרים
hs:2\d+       # מאה עשרים ואחת
hs:[^012]\d+  # מאה שלושים ושתיים
hs:0+         # מאה

1(\d\d) מאה $(hs:\1)$1
2(\d\d) מאתיים $(hs:\1)$1
(\d)(\d\d) $(df:$1) מאות $(hs:\2)$2

# separator after thousand
ts:00[^0]\d* ו # אלף ואחת
ts:01\d+ ו     # אלף ועשר
ts:020\d* ו    # אלף ועשרים
ts:02\d+       # אלף עשרים ואחת
ts:0[^012]\d+  # אלף שלושים ושתיים
ts:0+          # אלף

1(\d{3}) אלף $(ts:\1)$1
2(\d{3}) אלפיים $(ts:\1)$1
([3-9]|10)(\d{3}) $(dm:$1) אלפים $(ts:\2)$2
(\d{1,3})(\d{3}) $(m:$1) אלף $(ts:\2)$2

# separator function
:0+       # מיליון
#:\d+ "," # מיליון, אלף

1(\d{6}) מיליון$(:\1) $1
2(\d{6}) שני מיליונים$(:\1) $1
([3-9]|10)(\d{6}) $(m:$1) מיליונים$(:\2) $2
(\d{1,3})(\d{6}) $(m:$1) מיליון$(:\2) $2

1(\d{9}) מיליארד$(:\1) $1
2(\d{9}) שני מיליארדים$(:\1) $1
([3-9]|10)(\d{9}) $(m:$1) מיליארדים$(:\2) $2
(\d{1,3})(\d{9}) $(m:$1) מיליארד$(:\2) $2

1(\d{12}) טריליון$(:\1) $1
2(\d{12}) שני טריליונים$(:\1) $1
([3-9]|10)(\d{12}) $(m:$1) טריליונים$(:\2) $2
(\d{1,3})(\d{12}) $(m:$1) טריליון$(:\2) $2

1(\d{15}) קוודראליון$(:\1) $1
2(\d{15}) שני קוודראליונים$(:\1) $1
([3-9]|10)(\d{15}) $(m:$1) קוודראליונים$(:\2) $2
(\d{1,3})(\d{15}) $(m:$1) קוואדראליון$(:\2) $2

1(\d{18}) קווינטיליון$(:\1) $1
2(\d{18}) שני קווינטיליונים$(:\1) $1
([3-9]|10)(\d{18}) $(m:$1) קווינטיליונים$(:\2) $2
(\d{1,3})(\d{18}) $(m:$1) קווינטיליון$(:\2) $2

1(\d{21}) סקסטיליון$(:\1) $1
2(\d{21}) שני סקסטיליונים$(:\1) $1
([3-9]|10)(\d{21}) $(m:$1) סקסטיליונים$(:\2) $2
(\d{1,3})(\d{21}) $(m:$1) סקסטיליון$(:\2) $2

1(\d{24}) ספטיליון$(:\1) $1
2(\d{24}) שני ספטיליונים$(:\1) $1
([3-9]|10)(\d{24}) $(m:$1) ספטיליונים$(:\2) $2
(\d{1,3})(\d{24}) $(m:$1) ספטיליון$(:\2) $2


# negative number

[-−](\d+) מינוס |$1

# decimals

"([-−]?\d+)[.,]" $1| נקודה
"([-−]?\d+[.,])([^0]\d)" $1| |$2
"([-−]?\d+[.,])(\d)(\d)(\d)" $1| |$2 |$3 |$4
"([-−]?\d+[.,]\d*)(\d)" $1| |$2

# feminine->masculine correction, as all numbers are given for feminine gender

m:(.*)אחת \1אחד
m:(.*)שתיים \1שניים
m:(.*)חמש \1חמישה
m:(.*)שש \1שישה
m:(.*)(שלוש|ארבע|שבע|תשע|עשר) \1\2ה

m:(.*)אחת-עשרה \1אחד-עשר
m:(.*)שתים-עשרה \1שנים-עשר
m:(.*)חמש-עשרה \1חמישה-עשר
m:(.*)שש-עשרה \1שישה-עשר
m:(.*)(שלוש|ארבע|שבע|תשע)-עשרה \1\2ה-עשר

[fm]:(.*) \1

# numbers dependent on what they count

df:(.*)שתיים \1שתי

dm:(.*)שתיים \1שני
dm:(.*)שמונה \1שמונת
dm:(.*)(שלוש|ארבע|חמש|שש|שבע|תשע|עשר) \1\2ת

d([fm]):(.*) $(\1:\2)

# numbers independent on what they count

i([fm]):(.*) $(\1:\2)


# currency

# gender unit, gender subunit, unit singular, unit, plural, subunit singular, subunit, plural

us(.).:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) $(\1:\6) \2
up(.).:([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) $(\1:\6) \3
ss.(.):([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) $(\1:\6) \4
sp.(.):([^,]*),([^,]*),([^,]*),([^,]*),([^,]*) $(\1:\6) \5

# "mm" means masculine unit and masculine subunit

CHF:(.+),(.+) $(\1mm: פרנק שוויצרי, פרנקים שוויצריים, סנט, סנטים, \2)
CNY:(.+),(.+) $(\1mm: יואן, יואנים, פן, פנים, \2)
EUR:(.+),(.+) $(\1mm: אירו, אירו, סנט, סנטים, \2)
GBP:(.+),(.+) $(\1fm: לירה סטרלינג, לירות סטרלינג, פני, פנים, \2)
ILS:(.+),(.+) $(\1mf: שקל, שקלים, אגורה, אגורות, \2)
JPY:(.+),(.+) $(\1mm: יין, יינים, סן, סנים, \2)
USD:(.+),(.+) $(\1mm: דולר ארה"ב, דולרים ארה"ב, סנט, סנטים, \2)

xxxx 21!!!
"([A-Z]{3}) ([-−]?1)([.,]00?)?" $(\1:us,|$2)
"([A-Z]{3}) ([-−]?\d+)([.,]00?)?" $(\1:up,|$2)

"(CNY [-−]?\d+)[.,]10?" $1| ז'יאו אחד
"(CNY [-−]?\d+)[.,](\d)0?" $1| $2| ז'יאו
"(CNY [-−]?\d+[.,]\d)1" $1| $2| פן
"(CNY [-−]?\d+[.,]\d)(\d)" $1| $2| פן

xxxxx 21!!!
xxxxx 0.6!!!!
"(([A-Z]{3}) [-−]?\d+)[.,](01)" $1| ו$(\2:ss,$(1))
"(([A-Z]{3}) [-−]?\d+)[.,](\d)" $1| ו$(\2:sp,$(\30))
"(([A-Z]{3}) [-−]?\d+)[.,](\d\d)" $1| ו$(\2:sp,$3)

