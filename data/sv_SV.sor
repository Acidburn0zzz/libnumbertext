__numbertext__
^0 noll
^1$ ett
^1 en
1 ett
2 två
3 tre
4 fyra
5 fem
6 sex
7 sjo
8 åtta
9 nio
10 tio
11 elva
12 tolv
13 tretton
14 fjorton
15 femton
16 sexton
17 sjutton
18 arton
19 nitton
2(\d) tjugo$1
4(\d) fyrtio$1
7(\d) sjuttio$1
8(\d) åttio$1
9(\d) nittio$1
(\d)(\d) $1tio$2
1(\d\d) etthundra$1
(\d)(\d\d) $1hundra$2
1(\d{3}) ettusen $1
(\d{0,2}1)(\d{3}) $1usen $2
(\d{1,3})(\d{3}) $1tusen $2
1(\d{6}) en miljon $1
(\d{1,3})(\d{6}) $1 miljoner $2
1(\d{9}) en miljard $1
(\d{1,3})(\d{9}) $1 miljarder $2
1(\d{12}) en biljon $1
(\d{1,3})(\d{12}) $1 biljoner $2
1(\d{15}) en biljard $1
(\d{1,3})(\d{15}) $1 biljarder $2
1(\d{18}) en triljon $1
(\d{1,3})(\d{18}) $1 triljoner $2
1(\d{21}) en triljard $1
(\d{1,3})(\d{21}) $1 triljarder $2
1(\d{24}) en kvadriljon $1
(\d{1,3})(\d{24}) $1 kvadriljoner $2

# negative number

[-−](\d+) minus |$1

# decimals

"([-−]?\d+)[.,]" $1| komma
"([-−]?\d+[.,])(\d{1,2})" $1| |$2
"([-−]?\d+[.,])(\d)(\d)(\d)" $1| |$2 |$3 |$4
"([-−]?\d+[.,]\d*)(\d)" $1| |$2

# currency

# unit/subunit singular/plural

us:([^,]*),([^,]*),([^,]*),([^,]*) \1
up:([^,]*),([^,]*),([^,]*),([^,]*) \2
ss:([^,]*),([^,]*),([^,]*),([^,]*) \3
sp:([^,]*),([^,]*),([^,]*),([^,]*) \4

CHF:(\D+) $(\1: schweizisk franc, schweizisk franc, centime, centime)
CNY:(\D+) $(\1: yuan renminbi, yuan renminbi, fen, fen)
EUR:(\D+) $(\1: euro, euro, cent, cent)
GBP:(\D+) $(\1: brittiskt pund, brittiskt pund, penny, pence)
JPY:(\D+) $(\1: yen, yen, sen, sen)
SEK:(\D+) $(\1: svensk krona, svensk krona, öre, öre)
USD:(\D+) $(\1: US-dollar, US-dollar, cent, cent)

"([A-Z]{3}) ([-−]?1)([.,]00?)?" $2 $(\1:us)
"([A-Z]{3}) ([-−]?\d+)([.,]00?)?" $2 $(\1:up)

"(CNY [-−]?\d+)[.,]10?" $1 $2 jiao
"(CNY [-−]?\d+)[.,](\d)0?" $1 $2 jiao
"(CNY [-−]?\d+[.,]\d)1" $1 $2 fen
"(CNY [-−]?\d+[.,]\d)(\d)" $1 $2 fen

"(([A-Z]{3}) [-−]?\d+)[.,](01)" $1 |$(1) $(\2:ss)
"(([A-Z]{3}) [-−]?\d+)[.,](\d)" $1 |$(\30) $(\2:sp)
"(([A-Z]{3}) [-−]?\d+)[.,](\d\d)" $1 |$3 $(\2:sp)

