# Dutch number name patterns
# described by László Németh
# with help from Ruud Baars.
# version 2009-05-08

^0$ nul
1$ een
1 eenen
2$ twee
2 tweeën
3$ drie
3 drieën
4$ vier
4 vieren
5$ vijf
5 vijfen
6$ zes
6 zesen
7$ zeven
7 zevenen
8$ acht
8 achten
9$ negen
9 negenen

10 tien
11 elf
12 twaalf
13 dertien
14 veertien
1(\d) $1|tien

2(\d) $1twintig
3(\d) $1dertig
4(\d) $1veertig
8(\d) $1tachtig
(\d)(\d) $2$1|tig

1(\d\d) honderd$1
(\d)(\d\d) $1|honderd$2
10(\d{2}) duizend $1
(\d)0(\d{2}) $1|duizend $2
(1\d)(\d{2}) $1|honderd$2
(\d\d)(\d{2}) $1|honderd$2
(\d{2,3})(\d{3}) $1|duizend $2

(\d{1,3})(\d{6}) $1| miljoen $2
(\d{1,3})(\d{9}) $1| miljard $2
(\d{1,3})(\d{12}) $1| biljoen $2
(\d{1,3})(\d{15}) $1| biljard $2
(\d{1,3})(\d{18}) $1| triljoen $2
(\d{1,3})(\d{21}) $1| triljard $2

# negative number

[-−](\d\d*) min |$1

# decimals

"([-−]?\d\d*)[.,]00" |$1|
"([-−]?\d\d*)[.,](\d)(\d)" |$1| komma |$2| $3

# currencies
	
EUR euro
[A-Z]{3} 
"([A-Z]{3}) ([-−]?\d\d*)" $2| $1
"([A-Z]{3} [-−]?\d\d*)[.,]00" $1|
"(EUR [-−]?\d\d*)[.,]0(\d)" $1| en \2 cent
"(EUR [-−]?\d\d*)[.,](\d\d)" $1| en \2 cent
"([A-Z]{3}) ([-−]?\d\d*[.,]\d\d)" $2| $1
