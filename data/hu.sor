^0 nulla
1 egy
2$ kettő
2 két
3 három
4 négy
5 öt
6 hat
7 hét
8 nyolc
9 kilenc
10 tíz
1(\d) tizen$1
20 húsz
2(\d) huszon$1
3(\d) harminc$1
4(\d) negyven$1
5(\d) ötven$1
6(\d) hatvan$1
7(\d) hetven$1
8(\d) nyolcvan$1
9(\d) kilencven$1
1(\d\d) száz$1
(\d)(\d\d) $1száz$2
11(\d{2}) ezeregy$(1\1)
1(\d{3}) ezer$1

(\d{1,3})(\d{3}) $1ezer[-$2]
(\d{1,3})(\d{6}) $1millió[-$2]
(\d{1,3})(\d{9}) $1milliárd[-$2]
(\d{1,3})(\d{12}) $1billió[-$2]
(\d{1,3})(\d{15}) $1billiárd[-$2]
(\d{1,3})(\d{18}) $1trillió[-$2]
(\d{1,3})(\d{21}) $1trilliárd[-$2]

# negative numbers

[-−](\d+) mínusz |$1

# decimals

"([-−]?\d+)[.,](\d)" |$1| egész |$2 tized
"([-−]?\d+)[.,](\d\d)" |$1| egész |$2 század
"([-−]?\d+)[.,](\d{3})" |$1| egész |$2 ezred
"([-−]?\d+)[.,](\d)(\d)(\d)(\d)" |$1| egész |$2| |$3| |$4| |$5|
"([-−]?\d+[.,]\d+)(\d)" $1 |$2|

# currency

# unit/subunit

u:([^,]*),([^,]*)	\1
s:([^,]*),([^,]*)	\2

AUD:(.)	$(\1: ausztrál dollár, cent)
BGN:(.)	$(\1: bolgár leva, sztotinka)
BRL:(.)	$(\1: brazil real, centavo)
CAD:(.)	$(\1: kanadai dollár, cent)
CHF:(.)	$(\1: svájci frank, rappen)
CNY:(.)	$(\1: kínai jüan, fen)
CZK:(.)	$(\1: cseh korona, haléř)
DKK:(.)	$(\1: dán korona, øre)
EEK:(.)	$(\1: észt korona, sent)
EUR:(.)	$(\1: euró, cent)
GBP:(.)	$(\1: font sterling, penny)
HKD:(.)	$(\1: hongkongi dollár, cent)
HRK:(.)	$(\1: horvát kuna, lipa)
HUF:(.)	$(\1: forint, fillér)
ISK:(.)	$(\1: izlandi korona, eyrir)
JPY:(.)	$(\1: japán jen, szen)
KRW:(.)	$(\1: dél-koreai von, cson)
LTL:(.)	$(\1: litván litas, centas)
LVL:(.)	$(\1: lett lat, santīm)
MXN:(.)	$(\1: mexikói peso, centavo)
NOK:(.)	$(\1: norvég korona, øre)
NZD:(.)	$(\1: új-zélandi dollár, cent)
PLN:(.)	$(\1: lengyel złoty, grosz)
RON:(.)	$(\1: román lej, bani)
RSD:(.)	$(\1: szerb dinár, para)
RUB:(.)	$(\1: orosz rubel, kopejka)
SEK:(.)	$(\1: svéd korona, öre)
SGD:(.)	$(\1: szingapúri dollár, cent)
TRY:(.)	$(\1: török líra, kuruş)
UAH:(.)	$(\1: ukrán hrivnya, kopijka)
USD:(.)	$(\1: USA-dollár, cent)
ZAR:(.)	$(\1: dél-afrikai rand, cent)

"(JPY [-−]?\d+)[.,](\d\d)0" $1
"(JPY [-−]?\d+[.,]\d\d)(\d)" $1 $2 rin

"([A-Z]{3}) ([-−]?\d+)([.,]00?)?" $2$(\1:u)

"(CNY [-−]?\d+)[.,](\d)0?" $1 $2 jiao
"(CNY [-−]?\d+[.,]\d)(\d)" $1 $2 fen

"(([A-Z]{3}) [-−]?\d+)[.,](\d)" $1 $(\30)$(\2:s)
"(([A-Z]{3}) [-−]?\d+)[.,](\d\d)" $1 $3$(\2:s)


# Article a/az (number name)

"(article |cardinal |formal ){3}(([a-zA-Z]+ )*1\d{2}([.,]\d*)?)" az $(formal \2)
"(article |cardinal ){2}(([a-zA-Z]+ )*1(\d{3})*([.,]\d*)?)" az $2
"(article |cardinal ){2}(([a-zA-Z]+ )*5.*)" az $2
"(article |cardinal ){2}(.*)" a $2


# Article a/az + suffixation

"article (-[^0-9]+) ([{\(\[]?(\d+|[a-zA-Z])[}\)\]]?)" $(article $(\1 \2))

# Article a/az (number)

== article ==

([{\(\[]?(1(\d{3}|\d{6})*|[aefilmnorsuxyAEFILMNORSUXY])[}\)\]]?(-.*)?)	az \1
([{\(\[]?5.*)		az \1
(.*)			a \1

== ordinal ==

1 első
2 második
"(([a-zA-Z]+ )*[-−]?\d+)" $(ordinal $1)

(.*)nulla	\1nulladik
(.*)egy	\1egyedik
(.*)kettő	\1kettedik
(.*)három	\1harmadik
(.*)négy	\1negyedik
(.*)öt	\1ötödik
(.*)hat	\1hatodik
(.*)hét	\1hetedik
(.*)nyolc	\1nyolcadik
(.*)kilenc	\1kilencedik
(.*)tíz	\1tizedik
(.*)húsz	\1huszadik
(.*)harminc	\1harmincadik
(.*)(negy|öt|het|kilenc)ven	\1\2venedik
(.*)(hat|nyolc)van	\1\2vanadik
(.*)száz	\1századik
(.*)ezer	\1ezredik
(.*)illió	\1illiomodik
(.*)illiárd	\1illiárdodik

== formal ==

"(([a-zA-Z]+ )*[-−]?\d+([.,]\d*)?)" $(formal |$1)
(|.*-)((száz|ezer).*) $(formal \1egy\2)
(.*)két(.*) $(formal \1kettő\2)
(.*) \1

== year ==

# no hyphen in numbers

"(([a-zA-Z]+ )*[-−]?\d+)" $(year |$1)
(.*)-(.*) $(year \1\2)
(.*) \1

== cardinal ==

(.*) $1

== ordinal-number ==

(.*) \1.

== text ==

(\d|10) $1
(\d{2,4}) \1
(\d{2,3})(\d{3}) \1 ezer[ $(text \2)]
(\d{1,3})(\d{6}) \1 millió[ $(text \2)]
(\d{1,4})(\d{9}) \1 milliárd[ $(text \2)]
(\d{1,4})(\d{12}) \1 billió[ $(text \2)]
(\d{1,4})(\d{15}) \1 billiárd[ $(text \2)]

# suffix functions for numbers, dates, letters and parenthesized references
# -a, -áig, -ától, -ba, -ban, -ból, -d, -dik, -hoz, -nak, -nál, -n, -ra, -ról, -s, -szor, -szoros, -t, -tól, -ul, -vá, -vé
# for example:
#   -t 2 -> 2-t
#   -t 3 -> 3-at
#   -t 4 -> 4-et
#   -t 5 -> 5-öt
#   -t 6 -> 6-ot
#   -ban a) -> a)-ban
#   -ban b) -> b)-ben

== -a ==

"(1|.* 1)"	\1-je
"(2|.* 2)"	\1-a

== -ai ==

"(1|.* 1)"	\1-jei
0"(2|.* 2)"	\1-ai

== -i ==

"(1|.* 1)"	\1-ji

== -án ==

"(1|.* 1)"	\1-jén
"(2|.* 2)"	\1-án

== -áig ==

"(1|.* 1)"	\1-jéig
"(2|.* 2)"	\1-áig

== -ától ==

"(1|.* 1)"	\1-jétől
"(2|.* 2)"	\1-ától

== -d ==

([{\(\[]?(.*[^0]0{6,8}|.*[^0]0{12,14})[}\)\]]?)	\1-od
([{\(\[]?[ahkoquAHKOQU][}\)\]]?)	\1-ad
([{\(\[]?[bcdefgijlmnprstvwxzBCDEFGIJLMNPRSTVWXZ][}\)\]]?)	\1-ed

== -([ds]) ==

([{\(\[]?.*2[}\)\]]?)	\2-e\1
([{\(\[]?[yY][.]?[}\)\]]?)	\2-o\1

== -([dst]) ==

([{\(\[]?.*5[}\)\]]?)		\2-ö\1
([{\(\[]?(0|.*2|.*[^0]0{6,8}|.*[^0]0{12,14}|[abcdeghijkopqtuvwyzABCDEGHIJKOPQTUVWYZ][.]?)[}\)\]]?)	\2-\1
([{\(\[]?(.*6|.*[^0]0{9,11}|.*[^0]0{15,17})[}\)\]]?)	\2-o\1
([{\(\[]?(\d+|[a-zA-Z])[}\)\]]?)	$(-a\1 \2)

== -dik ==

(.?(\d+|[a-zA-Z][.]?)[}\)\]]?)		$(-d \1)ik

== -hoz ==

((.*2|.*5)[}\)\]]?)	\1-höz

== -szoros ==

(.*5[}\)\]]?)		\1-szörös

== -szor ==

([{\(\[]?(\d+|[a-zA-Z])[}\]\)]?)	$(-szor $(-szoros \1))
(.*-sz.r).s		\1

== -ig ==

szerda	szerdáig
((.*\d|[{\(\[]?[a-zA-Z])[}\)\]]?)	\1-ig
(.*\D)	\1ig

== -tól ==

szerda	szerdától
(hétfő|kedd|csütörtök|péntek|(szeptem|októ|novem|decem)ber)	\1től
(szombat|vasárnap|(jan|febr)uár|március|április|május|jú(n|l)ius|augusztus)	\1tól

== -ban ==

((szeptem|októ|novem|decem)ber)	\1ben
((jan|febr)uár|március|április|május|jú(n|l)ius|augusztus)	\1ban

== -n ==

hétfő		hétfőn
(kedd|péntek)	\1en
szerda		szerdán
csütörtök	csütörtökön
szombat		szombaton
vasárnap	vasárnap

(.*5[}\)\]]?)		\1-ön
([{\(\[]?(0|.*2|.*[^0]0{6,8}|.*[^0]0{12,14}|[abcdeghijkopqtuvwzABCDEGHIJKOPQTUVWZ][.]?)[}\)\]]?)	\1-n
(.?(\d+|[flmnrsxyFLMNRSXY][.]?)[}\)\]]?)		$(-on \1)

== -val ==

([{\(\[]?(0|.*[^0]0{6,8}|.*[^0]0{12,14}|[ahkoquAHKOQU][.]?)[}\)\]]?)	\1-val
(.*[14][}\)\]]?)	\1-gyel
(.*(2|[bcdegijptvwzBCDEGIJPTVWZ][.]?)[}\)\]]?)		\1-vel
(.*3[}\)\]]?)		\1-mal
(.*[57][}\)\]]?)	\1-tel
(.*6[}\)\]]?)		\1-tal
((.*8|.*30)[}\)\]]?)	\1-cal
(.*9[}\)\]]?)		\1-cel
(.*10[}\)\]]?)		\1-zel
(.*20[}\)\]]?)		\1-szal
(.*[4579]0[}\)\]]?)	\1-nel
(.*([68]0|[yY][.]?)[}\)\]]?)	\1-nal
(.*[^0]00[}\)\]]?)	\1-zal
(.*[^0]0{3,5}[}\)\]]?)	\1-rel
((.*[^0]0{9,11}|.*[^0]0{15,17})[}\)\]]?)	\1-dal
(.*([flmnrsFLMNRS])[.]?[}\)\]]?)	\1-\2el
(.*[xX][.]?[}\)\]]?)	\1-szel

== -vá ==

([{\(\[]?(\d+|[a-zA-Z])[}\)\]]?)		$(-vá $(-val \1))
(.*)al		\1á
(.*)el		\1é

== (-a|-ad|-ai|-án|-áig|-ban?|-ból|-hoz|-i|-nak|-nál|-on|-ra|-ról|-as|-szoros|-at|-á?tól|-ul) ==

# 1-be, 2-nek, 4-et etc., b)-ben, c)-ből, d)-hez, n-et etc.
# add affix after conversion to the alternative form

([{\(\[]?(.*[124579]|.*[14579]0|.*[^0]0{3,5}|[bcdefgijlmnprstvwxzBCDEFGIJLMNPRSTVWXZ][.]?)[}\)\]]?) \2$(\1 \1)

# otherwise: 0-ba, 3-at, 6-nál etc., a)-ban, h)-ból, k-hoz, y-ról etc.

([{\(\[]?(.*[0-9]+|[ahkoquyAHKOQUY][.]?)[}\)\]]?) \2\1

# conversion to the alternative form: -ban -> -ben etc.

-szoros		-szeres
-(.*)[ao](.*)	-\2e\3
-([brt])ól	-\2ől
-ától		-étől
-(.*)[á](.*)	-\2é\3
-ul		-ül

== help ==

"" $(1)|, $(2)|, $(3)\n$(\0 ordinal)$(\0 ordinal-number)year: $(year 2001), $(year 2002)|, $(year 2003)\nformal: $(formal 100), $(formal 200), $(formal 1000)\ntext: $(text 1), $(text 12000), $(text 10000000000)\ncurrency \(for example, HUF\): $(HUF 2,5)\nformal HUF: $(formal HUF 102,5)\nSuffixation of numbers and letters: -a, -án, -áig, -ától, -ba, -ban, -ból, -d, -dik, -hoz,\n    -nak, -nál, -n, -ra, -ról, -s, -szor, -szoros, -t, -tól, -ul, -vá, -val,\n    for example: -szor 15 -> $(-szor 15)\narticle: $(article 5)\narticle cardinal: $(article cardinal 100)\narticle cardinal formal: $(article cardinal formal 100)\narticle -ban: $(article -ban a\))

"(ordinal(-number)?|USD)" \1: $(\1 1), $(\1 2), $(\1 3)\n

